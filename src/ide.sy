#!/usr/bin/env spry

# Initialize libui
uiInit

# Build menu
menu = newMenu "File"
item = (menu menuAppendItem: "Open")
menu onShouldQuit: [do closeHandler]
item onMenuItemClicked: [echo (win openFile)]
item = (menu menuAppendItem: "Save")
item onMenuItemClicked: [echo (win saveFile)]
item = (menu menuAppendQuitItem)
editMenu = newMenu "Edit"
item = (editMenu menuAppendCheckItem: "Checkable Item")
editMenu menuAppendSeparator
item = (editMenu menuAppendItem: "Disabled Item")
item menuItemDisable
editMenu menuAppendPreferencesItem
helpMenu = newMenu "Help"
item = (helpMenu menuAppendItem: "Help")
helpMenu menuAppendAboutItem

# Create a new Window
win = newWindow "Rely on Spry!" 640 400 true
win windowMargin: 1

# Create a multiline text entry field for code
code = newMultilineEntryText

# And one for output
out = newMultilineEntryText

# And a vertical box to put stuff in
layout = newVerticalBox
buttons = newHorizontalBox

# Function to parse and evaluate code
run = func [do parse ("[", (code text), "]")]

# Some buttons and their handlers
doit = newButton "Do it"
doit onClicked: [run]
printit = newButton "Print it"
printit onClicked: [
  # Append result to out text field
  out append: ("\x0A", (run print))
]
clearit = newButton "Clear"
clearit onClicked: [out text: ""]
quitit = newButton "Quit"
quitit onClicked: [
  do closeHandler
]

# Group
group = newGroup "Workspace"
group groupMargin:: 1
group groupSetChild: layout

# Put things in the boxes
layout padding: 1
layout append: code stretch: 1
layout append: out stretch: 1
layout append: buttons stretch: 0
buttons append: doit stretch: 0
buttons append: printit stretch: 0
buttons append: clearit stretch: 0
buttons append: quitit stretch: 0

# Add box to window
win windowSetChild: group

# Set initial text
code text: "3 + 4"

# Close handler
closeHandler = [
  win message: "Thanks for trying it out!" title: "Spry cross-platform IDE!"
  controlDestroy win
  uiQuit
]

# Set a handler on closing window
win onClosing: closeHandler

# Show the window
win show

# Enter libui's event loop
win uiMain
